
dependencies {
    // to add new dependencies for compile add them in allDependenciesForPlatform method
    for (def dependency : allDependenciesForPlatform(platform(), arch())) {
        compile dependency
    }
}

def arch() {
    def arch = System.getProperty('os.arch')
    switch (arch) {
        case "x86": case "x86_64": return arch
        case "amd": return "x86"
        case "amd64": return "x86_64"
    }
    return arch
}

def platform() {
    def platform = System.getProperty('os.name').toLowerCase().split()[0]
    switch (platform) {
        case "mac": return "cocoa.macosx"
        case "linux": return "gtk.linux"
        case "windows": return "win32.win32"
    }
    return platform
}

def allDependenciesForPlatform(String platform, String arch) {
    def dependencies = []

    dependencies += project(':core')
    dependencies += "org.eclipse.swt:org.eclipse.swt.${platform}.${arch}:4.3"

    return dependencies
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

def bintrayUser = System.getenv("BINTRAY_USER")
if (bintrayUser == null) {
    bintrayUser = "jeancharles-roger"
}

def bintrayKey = System.getenv("BINTRAY_KEY")

subprojects {
    apply plugin: "java"
    apply plugin: 'maven-publish'

    group 'fr.minibilles.basics'
    version '1.0.2'

    repositories {
        jcenter()
    }

    dependencies {
        testCompile "junit:junit:4.12"
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        from javadoc.destinationDir
        classifier = 'javadoc'
    }

    publishing {
        repositories {
            maven {
                url "https://api.bintray.com/content/${bintrayUser}/maven/${group}/${version}"
                credentials {
                    username = bintrayUser
                    password = bintrayKey
                }
            }
        }
    }
}
